plugins {
    id 'java'
    id 'org.springframework.boot' version libs.versions.spring.boot.version
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

group 'org.apitome.sample.reactive'
version '1.0.0'
sourceCompatibility = '1.11'
targetCompatibility = '1.11'

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencyManagement {
    imports {
        mavenBom libs.spring.boot.bom.get().toString()
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation project(':core')

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation libs.bundles.junit
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    integrationTestImplementation 'io.cucumber:cucumber-java:6.10.4'
    integrationTestImplementation 'io.cucumber:cucumber-junit:6.10.4'
    integrationTestImplementation 'org.junit.vintage:junit-vintage-engine:5.7.2'
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events "passed"
    }
}